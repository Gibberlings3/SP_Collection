BACKUP ~weidu_external/spstuff/backup~ // location to store files for uninstall purposes
SUPPORT ~https://www.gibberlings3.net/forum/28-miscellaneous-released-mods/~ // email address displayed if install fails

VERSION ~v12~

README ~spstuff/lang/%LANGUAGE%/readme-spstuff.html~ ~spstuff/readme-spstuff.html~

ALWAYS

  ACTION_IF ((FILE_EXISTS ~dlc/sod-dlc.zip~) OR (FILE_EXISTS ~sod-dlc.zip~)) THEN BEGIN FAIL @3 END // DLC Merger check
  ACTION_IF NOT VARIABLE_IS_SET enhanced_edition THEN BEGIN // check to make this happen only once per install
    INCLUDE ~spstuff/lib/fl#add_kit_ee.tpa~         // ee kit stuff
    INCLUDE ~spstuff/lib/macro_spell_to_innate.tph~ // macro for innate conversion
  
    ACTION_IF GAME_IS ~eet bgee bg2ee iwdee~ THEN BEGIN // ee game

      OUTER_SET enhanced_edition = 1
      
      OUTER_SPRINT ~tra_location~ ~spstuff/lang~
  
      LOAD_TRA ~spstuff/lang/%LANGUAGE%/ee.tra~
  
    END ELSE BEGIN
    
      OUTER_SET enhanced_edition = 0
    
      OUTER_SPRINT ~tra_location~ ~weidu_external/spstuff/lang~

      // convert strings from UTF-8 for originals  
      ACTION_DEFINE_ARRAY cdnoconvert BEGIN weidu ee END
      ACTION_DEFINE_ARRAY cdreload BEGIN game END
      LAF HANDLE_CHARSETS INT_VAR from_utf8 = 1 infer_charsets = 1 
                          STR_VAR default_language = ~english~ tra_path = ~spstuff/lang~ out_path = ~weidu_external/spstuff/lang~ noconvert_array = cdnoconvert reload_array = cdreload END

      ACTION_FOR_EACH file IN iplot01k.itm iplot04g.itm iplot04h.itm iplot04i.itm xr2400.are xr2600.are BEGIN
        DISABLE_FROM_KEY "%file%"
      END
      
    END
  END
  
END

LANGUAGE ~English~ ~english~ 
  ~spstuff/lang/english/weidu.tra~ // English
  ~spstuff/lang/english/game.tra~
LANGUAGE ~Deutsch (Übersetzung von Jester)~ ~german~
  ~spstuff/lang/english/weidu.tra~ // English
  ~spstuff/lang/english/game.tra~
  ~spstuff/lang/german/weidu.tra~  // German
  ~spstuff/lang/german/game.tra~
LANGUAGE ~Spanish (traducido por Clan DLAN)~ ~spanish~ 
  ~spstuff/lang/english/weidu.tra~ // English
  ~spstuff/lang/english/game.tra~ 
  ~spstuff/lang/spanish/weidu.tra~ // Spanish
  ~spstuff/lang/spanish/game.tra~
LANGUAGE ~Czech (translation by Adam Stavárek)~ ~czech~ 
  ~spstuff/lang/english/weidu.tra~ // English
  ~spstuff/lang/english/game.tra~
  ~spstuff/lang/czech/weidu.tra~   // Czech
  ~spstuff/lang/czech/game.tra~
LANGUAGE ~French (translation by Saiko from the D'Oghmatiques)~ ~french~ 
  ~spstuff/lang/english/weidu.tra~ // English
  ~spstuff/lang/english/game.tra~
  ~spstuff/lang/french/weidu.tra~  // French
  ~spstuff/lang/french/game.tra~
LANGUAGE ~Italian (translation by Aedan)~ ~italian~ 
  ~spstuff/lang/english/weidu.tra~ // English
  ~spstuff/lang/english/game.tra~ 
  ~spstuff/lang/italian/weidu.tra~  // Italian
  ~spstuff/lang/italian/game.tra~
LANGUAGE ~Russian (translation by subzero400)~ ~russian~
  ~spstuff/lang/english/weidu.tra~ // English
  ~spstuff/lang/english/game.tra~ 
  ~spstuff/lang/russian/weidu.tra~  // Russian
  ~spstuff/lang/russian/game.tra~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Sylvan Mystic kit                                \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @100 DESIGNATED 0
LABEL ~cd_spc_sylvan_mystic~

ADD_KIT ~CDSylMys~ 
  ~CDSylMys                1           1           0           0           1           0           1           0~
  ~CDSylMys 0 0 0 0 0 0 0 0 2 2 2 2 2 2 2 2 0 0 0 0 0 0 0 0 2 2 0 0 2 2 2 3 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
  ~CDSylMys                8       14      10      14      14      0~
  ~CDSylMys                -5      0       -4      0       0       0~
  ~CDSylMys                8       14      10      14      14      0~
  ~CDSylMys                8       14      10      14      14      0~
  ~CDSylMys                1       0       0       1       0       0       0       0       0~
  ~CDSylMys                0       0       0       0       0       0~
  ~spstuff/sylmys/cdsylmys.2da~
  ~K_R_E   K_R_HE~
  ~0x00004000 12~
  ~Ra0~
  ~LEAT14 * HELM16 BAG24 RING06 RING39 CLCK02 BOOT01 AMUL19 BRAC16 BELT07 * * * * * * * * *~
  SAY @101
  SAY @102
  SAY @103
LAF fl#add_kit_ee STR_VAR kit_name = CDSylMys END

// if DR > v3  is installed prior to spc
ACTION_IF ((FILE_EXISTS_IN_GAME ~cdnegpp.spl~) AND (FILE_EXISTS ~Divine_Remix/lib/macro_reindex_clab.tph~)) THEN BEGIN

  INCLUDE ~Divine_Remix/lib/macro_reindex_clab.tph~
  INCLUDE ~Divine_Remix/lib/macro_add_ranger_spells.tph~
  INCLUDE ~Divine_Remix/lib/macro_shift_ranger_spells.tph~
  COPY_EXISTING ~CDsylmys.2da~ ~override~
    LAUNCH_PATCH_MACRO ~add_ranger_spells~ // add generic cleric spells
    LAUNCH_PATCH_MACRO ~shift_ranger_spells~ // shift generic ranger spells
    LAUNCH_PATCH_MACRO ~reindex_clab~ // re-index lines
    BUT_ONLY

END

//Unusability hacks - non-ee games
//
//Basically, making items unusable by a particular kit is a pain in the ass, unless it is the same as an existing kit.
//
//First, we're changing the Helm cleric kit to have the same restrictions as the base cleric class, since there are no items that are restricted to non-Helmites. We're going to use the Helm kit unusability flag to our advantage. Basically, the Sylvan Mystic will use the Helm kit unusability flag for its restrictions, in addition to items not usable by rangers.
//
//One of the many problems to this approach is that it causes unexpected values for mods which interact with the Helm kit, which in turn may cause incompatibilities.

ACTION_IF !enhanced_edition THEN BEGIN

  COPY_EXISTING ~kitlist.2da~ ~override~
    REPLACE_TEXTUALLY ~0x02000000~ ~0x00004000~
    REPLACE_TEXTUALLY ~^\(%CDSylMys%[ %TAB%]+.+\)0x00004000\([ %TAB%]+12\)$~ ~\10x02004000\2~

END ELSE BEGIN

  OUTER_SET name = RESOLVE_STR_REF (@102)
  OUTER_SET kitno = CDSylMys + 0x4000

END

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  READ_SHORT 0x1c type
  READ_BYTE  0x20 ranger
  READ_BYTE  0x29 kit
  PATCH_IF ((type = 14) OR // Bullets
            (type = 17) OR // Maces
            (type = 18) OR // Slings
            (type = 21) OR // Hammers
            (type = 22) OR // Morningstars
            (type = 23) OR // Flails
            (type = 24) OR // Darts
            (type = 26) OR // Staves
            (type = 27) OR // Crossbows
            (type = 29) OR // Spears
            (type = 30) OR // Halberds
            (type = 31) OR // Bolts
            (type = 44) OR // Clubs
            ((((ranger BOR 0b11011111) = 0b11111111) OR // if unusable by ranger
              ((kit BOR 0b10111111) = 0b11111111)) AND  // or by barbarian
              ((type = 2)  OR // Armor
               (type = 60) OR // Leather Armor
               (type = 61) OR // Studded Leather Armor
               (type = 62) OR // Chain Mail
               (type = 63) OR // Splint Mail
               (type = 64) OR // Half plate
               (type = 65) OR // Full plate
               (type = 66) OR // Hide armor
               (type = 67)))) BEGIN // Robes
    PATCH_IF !enhanced_edition BEGIN
      WRITE_BYTE 0x29 (THIS BOR BIT1) // adds helm unusability flag
    END ELSE BEGIN
      LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 319 target = 1 parameter1 = kitno parameter2 = 9 timing = 2 special = name END
    END
  END ELSE BEGIN // if not one of the forbidden items, removes redundant Helm flag if present
    PATCH_IF (((ranger BAND BIT5) = 0) AND (!enhanced_edition)) BEGIN // usable by Ranger, non-EE game
      WRITE_BYTE 0x29 (THIS BAND  `BIT1) // removes helm flag
    END
  END
  BUT_ONLY

// Spells for Sylvan Mystic Kit
LAF cd_clone_spell INT_VAR make_innate = 1 STR_VAR source = sppr506 destination = ~CDsmisk~ END // iron skins innate
LAF cd_clone_spell INT_VAR make_innate = 1 STR_VAR source = sppr508 destination = ~CDsmchc~ END // chaotic commands innate

OUTER_SET aura = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_AURA_OF_FLAMING_DEATH~)) - 1000)
OUTER_SET deva = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_SUMMON_DEVA~)) - 1000)

ACTION_IF ((aura > 0) AND (FILE_EXISTS_IN_GAME ~sppr%aura%.spl~)) THEN BEGIN // if spell present
  
  LAF cd_clone_spell INT_VAR make_innate = 1 STR_VAR source = EVAL ~sppr%aura%~ destination = ~CDsmdva~ END // summon deva innate

END ELSE BEGIN   

  COPY ~spstuff/SylMys/SoA_copy~ ~override~ // bams, sounds, vvcs
  
  COPY ~spstuff/SylMys/CDsmafd.spl~  ~override/CDsmafd.spl~  // Aura of Flaming Death
       ~spstuff/SylMys/CDsmafdd.spl~ ~override/CDsmafdd.spl~ // Aura of Flaming Death backlash spell
    SAY NAME1 @104
    SAY NAME2 @104
    SAY UNIDENTIFIED_DESC @104
    SAY DESC @104

END 

ACTION_IF ((deva > 0) AND (FILE_EXISTS_IN_GAME ~sppr%deva%.spl~)) THEN BEGIN // if spell present
  
  LAF cd_clone_spell INT_VAR make_innate = 1 STR_VAR source = EVAL ~sppr%aura%~ destination = ~CDsmdva~ END // summon deva innate

END ELSE BEGIN  

  COPY_EXISTING ~cdsylmys.2da~ ~override~
    REPLACE_TEXTUALLY ~GA_CDSMDVA~ ~****      ~ // remove from table
	BUT_ONLY

  OUTER_SET string = 0 
  COPY_EXISTING ~kitlist.2da~ ~override~ // fetch kit description 
    REPLACE_EVALUATE ~^\([0-9]+[ %TAB%]+CDSylMys[ %TAB%]+[0-9]+[ %TAB%]+[0-9]+[ %TAB%]+\)\([0-9]+\)~ BEGIN
      SET string = ~%MATCH2%~
    END ~%MATCH1%%MATCH2%~
    BUT_ONLY
  
  ACTION_IF string THEN BEGIN // if zero, it means the R_E above didn't work for some reason 
    ACTION_GET_STRREF string description // remove line about summon deva, then update string
    OUTER_PATCH_SAVE ~description~ ~%description%~ BEGIN
      REPLACE_TEXTUALLY ~[ %TAB%%LNL%%MNL%%WNL%].*23.*+$~ ~~
    END
    STRING_SET_EVALUATE string ~%description%~  
  END	
  
END	

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Blade Master kit                                 \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @200 DESIGNATED 1
LABEL ~cd_spc_blade_master~

ADD_KIT ~CDBladeM~ 
  ~CDBladeM                1           1           0           0           0           0           0           0~
  ~CDBladeM 0 0 0 0 0 0 0 0 5 5 5 5 0 5 5 5 0 0 0 0 0 0 0 0 0 0 0 0 0 2 2 3 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
  ~CDBladeM                0       0       0       0       0       0~
  ~CDBladeM               -2       2       0       0       0       0~
  ~CDBladeM                0       0       0       0       0       0~
  ~CDBladeM                0       0       0       0       0       0~
  ~CDBladeM                1       1       1       1       1       1       1       1       1~
  ~CDBladeM                0       1       1       1       1       0~
  ~spstuff/BladeM/cdbladem.2da~
  ~K_F_H   K_F_E   K_F_HE~
  ~0x00010000 2~
  ~Fi0~
  ~CHAN09 * HELM07 BAG20 RING06 RING31 * BOOT01 AMUL19 BRAC16 BELT06 AROW11,40 BULL03,40 BOLT06,40 POTN52,5 POTN04,2 POTN14,5 SW1H23 SW1H27 SW2H12~
  SAY @201
  SAY @202
  SAY @203
LAF fl#add_kit_ee STR_VAR kit_name = CDBladeM END

COPY ~spstuff/BladeM/copy~ ~override~  // BAMs

COPY ~spstuff/BladeM/CDbmxat.spl~ ~override~  // Extra Attack
  SAY NAME1 @210
  SAY NAME2 @210
  SAY UNIDENTIFIED_DESC @210
  SAY DESC @210

COPY ~spstuff/BladeM/CDbmctr.spl~ ~override~  // Counter Attack
  SAY NAME1 @211
  SAY NAME2 @211
  SAY UNIDENTIFIED_DESC @211
  SAY DESC @211

COPY ~spstuff/BladeM/CDbmdrm.spl~ ~override~  // Disarm
  SAY NAME1 @212
  SAY NAME2 @212
  SAY UNIDENTIFIED_DESC @212
  SAY DESC @212

COPY ~spstuff/BladeM/CDbmfnt.spl~ ~override~  // Feint
  SAY NAME1 @213
  SAY NAME2 @213
  SAY UNIDENTIFIED_DESC @213
  SAY DESC @213

COPY ~spstuff/BladeM/CDbmpst.spl~ ~override~  // Precision Strike
  SAY NAME1 @214
  SAY NAME2 @214
  SAY UNIDENTIFIED_DESC @214
  SAY DESC @214

COPY ~spstuff/BladeM/CDbmpry.spl~ ~override~  // Parry
  SAY NAME1 @215
  SAY NAME2 @215
  SAY UNIDENTIFIED_DESC @215
  SAY DESC @215

COPY ~spstuff/BladeM/CDbmres.spl~ ~override~  // Inherent damage resistance
  SAY NAME1 @216
  SAY NAME2 @216
  SAY UNIDENTIFIED_DESC @216
  SAY DESC @216

COPY ~spstuff/BladeM/CDbmsws.spl~ ~override~  // Swift Strike
  SAY NAME1 @217
  SAY NAME2 @217
  SAY UNIDENTIFIED_DESC @217
  SAY DESC @217

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Death Knight kit                                 \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @300 DESIGNATED 2 
LABEL ~cd_spc_death_knight~

ADD_KIT ~CDDeathK~ 
  ~CDDeathK                1           1           1           1           1           1           1           1~
  ~CDDeathK 0 0 0 0 0 0 0 0 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 3 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
  ~CDDeathK               10       3       5      13       3       8~
  ~CDDeathK                0       0       0       0       0       0~
  ~CDDeathK               10       3       5      13       3       8~
  ~CDDeathK               10       3       5      13       3       8~
  ~CDDeathK                0       0       1       0       0       1       0       0       1~
  ~CDDeathK                0       0       0       0       0       0~
  ~spstuff/DeathK/CDdeathk.2da~
  ~K_F_H~
  ~0x00000009 2~
  ~Fi0~
  ~CHAN09 * HELM07 BAG20 RING06 RING31 * BOOT01 AMUL19 BRAC16 BELT06 AROW11,40 BULL03,40 BOLT06,40 POTN52,5 POTN04,2 POTN14,5 HAMM07 SW1H27 STAF08~
  SAY @301
  SAY @302
  SAY @303
LAF fl#add_kit_ee STR_VAR kit_name = CDDeathK END

// Adding abilities for the Death Knight Kit
LAF cd_clone_spell INT_VAR make_innate = 1 STR_VAR source = spwi501 destination = ~CDdkand~ END // animate dead innate
LAF cd_clone_spell INT_VAR make_innate = 1 STR_VAR source = spwi218 destination = ~CDdkght~ END // ghoul touch innate
LAF cd_clone_spell INT_VAR make_innate = 1 STR_VAR source = spwi119 destination = ~CDdklmd~ END // larloch's minor drain innate
LAF cd_clone_spell INT_VAR make_innate = 1 STR_VAR source = spwi314 destination = ~CDdkvmt~ END // vampiric touch innate

COPY ~spstuff/DeathK/copy~ ~override~  // BAMs

COPY ~spstuff/DeathK/CDdkebh.spl~ ~override~  // Ebony Hands
  SAY NAME1 @310
  SAY NAME2 @310
  SAY UNIDENTIFIED_DESC @311
  SAY DESC @311

COPY ~spstuff/DeathK/CDdkhmt.spl~ ~override~  // Harm Touch
  SAY NAME1 @312
  SAY NAME2 @312
  SAY UNIDENTIFIED_DESC @312
  SAY DESC @312

COPY ~spstuff/DeathK/CDdkhbl.spl~ ~override~  // Heart Blight
  SAY NAME1 @313
  SAY NAME2 @313
  SAY UNIDENTIFIED_DESC @314
  SAY DESC @314

COPY ~spstuff/DeathK/CDdkiss.spl~ ~override~  // Kiss of Torment Spell
  SAY NAME1 @315
  SAY NAME2 @315
  SAY UNIDENTIFIED_DESC @316
  SAY DESC @316

COPY ~spstuff/DeathK/CDdkiss.itm~ ~override~  // Kiss of Torment Touch Item
  SAY NAME1 @315
  SAY NAME2 @315
  SAY UNIDENTIFIED_DESC @315
  SAY DESC @315

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Wushi Ninja kit                                  \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @400 DESIGNATED 3
LABEL ~cd_spc_wushi_ninja~

ADD_KIT ~CDWushiN~
  ~CDWushiN                1           1           1           1           1           0           0           0~
  ~CDWushiN 0 0 0 0 0 0 0 0 0 2 2 0 0 2 2 2 0 2 0 0 0 0 2 2 0 2 2 2 1 1 1 3 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
  ~CDWushiN                3       9       3       3       3       3~
  ~CDWushiN                1       0       0       1       0      -2~
  ~CDWushiN                3       9       3       3       3       3~
  ~CDWushiN                3       9       3       3       3       3~
  ~CDWushiN                0       1       1       1       1       1       1       1       1~
  ~CDWushiN                1       1       1       0       0       0~
  ~spstuff/WushiN/CDwushin.2da~
  ~K_T_H~
  ~0x00004000 4~
  ~Th0~
  ~LEAT14 * * BAG28 RING06 RING05,10 * BOOT02 AMUL19 BRAC16 BELT06 AROW11,40 BULL03,40 BOLT06,40 POTN52,5 POTN04,2 POTN14,5 DAGG12 SW1H28 STAF08~
  SAY @401
  SAY @402
  SAY @403
LAF fl#add_kit_ee STR_VAR kit_name = CDWushiN 
                          thiefskl = ~CDWushiN 40 20~ END

// set dual-wielding pips to match generic thief
// in case Rogue Rebalancing or other mods have changed it
COPY_EXISTING ~weapprof.2da~ ~override~
  COUNT_2DA_COLS col_num
  READ_2DA_ENTRY 31 7             col_num dual
  SET_2DA_ENTRY  31 (col_num - 1) col_num dual
  BUT_ONLY

// Adding abilities for the Wushi Ninja Kit
LAF cd_clone_spell INT_VAR make_innate = 1 STR_VAR source = spwi217 destination = ~CDwnagn~ END // aganazzar's scorcher innate
LAF cd_clone_spell INT_VAR make_innate = 1 STR_VAR source = spwi201 destination = ~CDwnblr~ END // blur innate
LAF cd_clone_spell INT_VAR make_innate = 1 STR_VAR source = sppr103 destination = ~CDwnclw~ END // cure light wounds innate
LAF cd_clone_spell INT_VAR make_innate = 1 STR_VAR source = spwi112 destination = ~CDwnmgm~ END // magic missile innate
LAF cd_clone_spell INT_VAR make_innate = 1 STR_VAR source = spwi212 destination = ~CDwnmig~ END // mirror image innate
LAF cd_clone_spell INT_VAR make_innate = 1 STR_VAR source = spwi115 destination = ~CDwnshg~ END // shocking grasp innate
LAF cd_clone_spell INT_VAR make_innate = 1 STR_VAR source = spwi513 destination = ~CDwnbrc~ END // breach innate
LAF cd_clone_spell INT_VAR make_innate = 1 STR_VAR source = spwi522 destination = ~CDwnmst~ END // minor spell turning innate
LAF cd_clone_spell INT_VAR make_innate = 1 STR_VAR source = spwi505 destination = ~CDwnshd~ END // shadow door innate
    
COPY ~spstuff/WushiN/CDwnt10.spl~ ~override~ // THAC0 Adjustment
     ~spstuff/WushiN/CDwnt11.spl~ ~override~ // THAC0 Adjustment
     ~spstuff/WushiN/CDwnt12.spl~ ~override~ // THAC0 Adjustment
     ~spstuff/WushiN/CDwnt13.spl~ ~override~ // THAC0 Adjustment
     ~spstuff/WushiN/CDwnt14.spl~ ~override~ // THAC0 Adjustment
     ~spstuff/WushiN/CDwnt15.spl~ ~override~ // THAC0 Adjustment
     ~spstuff/WushiN/CDwnt16.spl~ ~override~ // THAC0 Adjustment
  SAY NAME1 @410
  SAY NAME2 @410
  SAY UNIDENTIFIED_DESC @410
  SAY DESC @410

ACTION_IF NOT FILE_EXISTS_IN_GAME ~thiefskl.2da~ THEN BEGIN // EE game with better way to limit thief points to 20/level

  COPY_EXISTING ~kitlist.2da~ ~override~
    REPLACE_TEXTUALLY ~^\(%CDWushiN%[ %TAB%]+.+\)0x00004000\([ %TAB%]+4\)$~ ~\10x00080000\2~
    BUT_ONLY

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Archer of Sylvanus kit                           \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @600 DESIGNATED 5
LABEL ~cd_spc_archer_of_sylvanus~

ADD_KIT ~CDArcSyl~ 
  ~CDArcSyl                0           0           1           0           1           0           0           0~
  ~CDArcSyl 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 1 0 0 0 0 0 3 3 3 0 0 0 1 1 1 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
  ~CDArcSyl                3       3       3       3      12      15~
  ~CDArcSyl                0       0       0       0       0       0~
  ~CDArcSyl                3       3       3       3      12      15~
  ~CDArcSyl                3       3       3       3      12      15~
  ~CDArcSyl                0       0       0       0       1       0       0       0       0~
  ~CDArcSyl                0       0       0       0       0       0~
  ~spstuff/ArcSyl/CDarcsyl.2da~
  ~K_D_HE~
  ~0x00004000 11~
  ~Dr0~
  ~LEAT14 * * BAG28 RING06 RING05,10 * BOOT02 AMUL19 BRAC16 BELT06 AROW11,40 BULL03,40 BOLT06,40 POTN52,5 POTN04,2 POTN14,5 * * *~
  SAY @601
  SAY @602
  SAY @603
LAF fl#add_kit_ee STR_VAR kit_name = CDArcSyl END

// if DR > v3  is installed prior to spc
ACTION_IF ((FILE_EXISTS_IN_GAME ~cdnegpp.spl~) AND (FILE_EXISTS ~Divine_Remix/lib/macro_reindex_clab.tph~)) THEN BEGIN

  INCLUDE ~Divine_Remix/lib/macro_reindex_clab.tph~
  INCLUDE ~Divine_Remix/lib/macro_add_druid_spells.tph~
  COPY_EXISTING ~CDarcsyl.2da~ ~override~
    LAUNCH_PATCH_MACRO ~add_druid_spells~ // add generic cleric spells
    LAUNCH_PATCH_MACRO ~reindex_clab~ // re-index lines
    BUT_ONLY

END

// Unusability hacks, non-ee games
//
//Basically, making items unusable by a particular kit is a pain in the ass, unless it is the same as an existing kit.
//
//First, we're changing the Undead Hunter paladin kit to have the same restrictions as the base paladin class, since there are no items that are restricted to non-Undead Hunters. We're going to use the Undead Hunter kit unusability flag to our advantage. Basically, the Archer of Sylvanus will have the same restrictions as a druid plus Undead Hunter-kit restrictions, which we're going to set in non-allowed items. To allow the kit to use bows, we also need to make them available to the base class.
//
//One of the many problems to this approach is that it causes unexpected values for mods which interact with the Undead Hunter kit, which in turn may cause incompatibilities.
ACTION_IF !enhanced_edition BEGIN // non-ee

  COPY_EXISTING ~kitlist.2da~ ~override~
    REPLACE_TEXTUALLY ~0x00000020~ ~0x00004000~
    REPLACE_TEXTUALLY ~^\(%CDArcSyl%[ %TAB%]+.+\)0x00004000\([ %TAB%]+11\)$~ ~\10x00004020\2~

END ELSE BEGIN

  OUTER_SET name = RESOLVE_STR_REF (@602)
  OUTER_SET kitno = CDArcSyl + 0x4000

  // annoyingly, i've discovered in EE kits can't place pips unless the parent class can place at least one pip in the prof
  COPY_EXISTING ~weapprof.2da~ ~override~
    SET_2DA_ENTRY_LATER ~weapprof~ 26 10 ~1~ // 1 star for druids in xbows
    SET_2DA_ENTRY_LATER ~weapprof~ 27 10 ~1~ // 1 star for druids in longbows
    SET_2DA_ENTRY_LATER ~weapprof~ 28 10 ~1~ // 1 star for druids in shortbows
    SET_2DA_ENTRIES_NOW ~weapprof~ 1
    PRETTY_PRINT_2DA
    BUT_ONLY

END

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  READ_SHORT 0x1c type
  READ_BYTE  0x20 ranger
  READ_BYTE  0x2b stalker
  READ_BYTE  0x31 prof
  PATCH_IF ((((type = 17) AND NOT (prof = 115)) OR // Maces excluding clubs
           (type = 16) OR  // Daggers
           (type = 18) OR  // Slings
           (type = 19) OR  // Short Swords
           (type = 20) OR  // Long Swords
           (type = 21) OR  // Hammers
           (type = 22) OR  // Morningstars
           (type = 23) OR  // Flails
           (type = 24) OR  // Darts
           (type = 25) OR  // Axes
           (type = 26) OR  // Staves
           (type = 29) OR  // Spears
           (type = 30) OR  // Halberds
           (type = 57) OR  // Great swords
           (type = 69)) OR // Bastard swords
           (((type = 2)  OR   // Armor
             (type = 60) OR   // Leather Armor
             (type = 61) OR   // Studded Leather Armor
             (type = 62) OR   // Chain Mail
             (type = 63) OR   // Splint Mail
             (type = 64) OR   // Half plate
             (type = 65) OR   // Full plate
             (type = 66) OR   // Hide armor
             (type = 67)) AND // Robes
            ((stalker BAND BIT0) = BIT0))) BEGIN
    PATCH_IF !enhanced_edition BEGIN
      WRITE_BYTE 0x2f (THIS BOR BIT6) // makes unusable by UD hunters
    END ELSE BEGIN
      LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 319 target = 1 parameter1 = kitno parameter2 = 9 timing = 2 special = name END
    END
  END
  PATCH_IF (((ranger BOR `BIT5) = `BIT5) AND // if usable by rangers and is one of the following four item types
         ((type = 5)   OR  // arrows
          (type = 15)  OR  // bows
          (type = 27)  OR  // xbows
          (type = 31))) BEGIN // bolts
    WRITE_BYTE 0x21 (THIS BAND `BIT6) // makes usable by druids
    PATCH_IF enhanced_edition BEGIN
      LPF DELETE_EFFECT INT_VAR check_headers = 0 match_opcode = 319 match_parameter1 = 11 match_parameter2 = 5 END // delete 319s vs druids
    END
  END
  BUT_ONLY

COPY ~spstuff/ArcSyl/copy~ ~override~  // BAMs

// Adding abilities for the Archer of Sylvanus Kit
COPY ~spstuff/ArcSyl/CDashid.spl~ ~override~  // Hide
  SAY NAME1 @610
  SAY NAME2 @610
  SAY UNIDENTIFIED_DESC @610
  SAY DESC @610

COPY ~spstuff/ArcSyl/CDasres.spl~ ~override~  // Resistances
  SAY NAME1 @611
  SAY NAME2 @611
  SAY UNIDENTIFIED_DESC @611
  SAY DESC @611

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Arcane Fist kit                                  \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @700 DESIGNATED 6
LABEL ~cd_spc_arcane_fist~

ADD_KIT ~CDArFist~ // Arcane Fist
  ~CDArFist                0           0           0           0           0           0           0           0~
  ~CDArFist 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
  ~CDArFist                3       3       3       9       3       3~
  ~CDArFist                0       0       0       0       0       0~
  ~CDArFist                3       3       3       9       3       3~
  ~CDArFist                3       3       3       9       3       3~
  ~CDArFist                1       1       1       0       0       0       0       0       0~
  ~CDArFist                0       0       0       0       0       0~
  ~spstuff/ArFist/cdarfist.2da~
  ~K_B_H~
  ~0x00004000 5~
  ~Mo0~
  ~* * * BAG28 RING06 RING05,10 * BOOT02 AMUL19 BRAC16 BELT06 * * * POTN52,5 POTN04,2 POTN14,5 * * *~
  SAY @701
  SAY @702
  SAY @703
LAF fl#add_kit_ee STR_VAR kit_name = CDArFist END

//Unusability hacks
//
//Basically, making items unusable by a particular kit is a pain in the ass, unless it is the same as an existing kit.
//
//First, we're changing the Talos cleric kit to have the same restrictions as the base cleric class, since there are no items that are restricted to non-Talosians. We're going to use the Talos kit unusability flag to our advantage. Basically, the Arcane Fist will have the same restrictions as a bard plus Talos-kit restrictions, which we're going to set in non-allowed items.
//
//One of the many problems to this approach is that it causes unexpected values for mods which interact with the Talos kit, which in turn may cause incompatibilities. Also, because of the hard-coded restrictions the only way to allow the Arcane Fist to wear robes is to allow all bards to wear robes.

ACTION_IF !enhanced_edition BEGIN // non-ee

  COPY_EXISTING ~kitlist.2da~ ~override~
    REPLACE_TEXTUALLY ~0x01000000~ ~0x00004000~
    REPLACE_TEXTUALLY ~^\(%CDArFist%[ %TAB%]+.+\)0x00004000\([ %TAB%]+5\)$~ ~\10x01004000\2~

END ELSE BEGIN

  OUTER_SET name = RESOLVE_STR_REF (@702)
  OUTER_SET kitno = CDArFist + 0x4000

END

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  READ_SHORT 0x1c type
  READ_BYTE  0x1f mage
  READ_BYTE  0x20 mage1
  PATCH_IF (((mage1 BAND BIT2) = 0) AND       // if usable by mages
           ((type = 67) OR (type = 2))) BEGIN // and is robe or armor
    WRITE_BYTE 0x1e (THIS BAND `BIT6)         // removes bard flag
  END
  PATCH_IF (((((type = 2) OR (type = 67)) AND ((mage BAND BIT2) = 0)) OR
      (type = 5)  OR // Arrows
      (type = 7)  OR // Headgear
      (type = 12) OR // Shields
      (type = 14) OR // Bullets
      (type = 15) OR // Bows
      (type = 16) OR // Daggers
      (type = 17) OR // Maces
      (type = 18) OR // Slings
      (type = 19) OR // Short Swords
      (type = 20) OR // Long Swords
      (type = 21) OR // Hammers
      (type = 22) OR // Morningstars
      (type = 23) OR // Flails
      (type = 24) OR // Darts
      (type = 25) OR // Axes
      (type = 26) OR // Staves
      (type = 27) OR // Crossbows
      (type = 29) OR // Spears
      (type = 30) OR // Halberds
      (type = 31) OR // Bolts
      (type = 41) OR // Bucklers
      (type = 44) OR // Clubs
      (type = 47) OR // Large Shields
      (type = 49) OR // Medium Shields
      (type = 53) OR // Small Shields
      (type = 57) OR // Great swords
      (type = 60) OR // Leather Armor
      (type = 61) OR // Studded Leather Armor
      (type = 62) OR // Chain Mail
      (type = 63) OR // Splint Mail
      (type = 64) OR // Half plate
      (type = 65) OR // Full plate
      (type = 66) OR // Hide armor
      (type = 69))) BEGIN // Bastard swords
    PATCH_IF !enhanced_edition BEGIN
      WRITE_BYTE 0x29 (THIS BOR BIT0) // adds talos flag
    END ELSE BEGIN
      LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 319 target = 1 parameter1 = kitno parameter2 = 9 timing = 2 special = name END
    END
  END
  BUT_ONLY

INCLUDE ~spstuff/lib/tob2soa.tph~ // adding all ToB stuff to SoA unless already present

// since fists are not saved, need to add scripting to baldur.bcs that will restore the fists after a reload
EXTEND_TOP ~baldur.bcs~ ~spstuff/ArFist/baldur.baf~

ACTION_IF FILE_EXISTS_IN_GAME ~baldur25.bcs~ THEN BEGIN

  EXTEND_TOP ~baldur25.bcs~ ~spstuff/ArFist/baldur.baf~

END

// Adding abilities and items for the Arcane Fist Kit
COPY ~spstuff/ArFist/CDafist1.itm~ ~override~  // Fist lvl 1-3
     ~spstuff/ArFist/CDafist2.itm~ ~override~  // Fist lvl 4-6
     ~spstuff/ArFist/CDafist3.itm~ ~override~  // Fist lvl 7-9
     ~spstuff/ArFist/CDafist4.itm~ ~override~  // Fist lvl 10-12
     ~spstuff/ArFist/CDafist5.itm~ ~override~  // Fist lvl 13-15
     ~spstuff/ArFist/CDafist6.itm~ ~override~  // Fist lvl 16+
  SAY NAME1 @710
  SAY NAME2 @710
  SAY UNIDENTIFIED_DESC @710
  SAY DESC @710

COPY ~spstuff/ArFist/CDafats.spl~ ~override~  // Extra Attack
  SAY NAME1 @712
  SAY NAME2 @712
  SAY UNIDENTIFIED_DESC @712
  SAY DESC @712

COPY ~spstuff/ArFist/CDafdist.spl~ ~override~  // Disable Pickpocket Button
  SAY NAME1 @713
  SAY NAME2 @713
  SAY UNIDENTIFIED_DESC @713
  SAY DESC @713

COPY ~spstuff/ArFist/CDafsas.spl~ ~override~  // +1 bonus to save vs spells
  SAY NAME1 @714
  SAY NAME2 @714
  SAY UNIDENTIFIED_DESC @714
  SAY DESC @714

COPY ~spstuff/ArFist/CDaft08.spl~ ~override~  // THAC0 Progression
     ~spstuff/ArFist/CDaft12.spl~ ~override~  // THAC0 Progression
     ~spstuff/ArFist/CDaft14.spl~ ~override~  // THAC0 Progression
     ~spstuff/ArFist/CDaft16.spl~ ~override~  // THAC0 Progression
     ~spstuff/ArFist/CDaft18.spl~ ~override~  // THAC0 Progression
  SAY NAME1 @715
  SAY NAME2 @715
  SAY UNIDENTIFIED_DESC @715
  SAY DESC @715

COPY ~spstuff/ArFist/CDaffis1.spl~ ~override~  // Fist Upgrade
     ~spstuff/ArFist/CDaffis2.spl~ ~override~  // Fist Upgrade
     ~spstuff/ArFist/CDaffis3.spl~ ~override~  // Fist Upgrade
     ~spstuff/ArFist/CDaffis4.spl~ ~override~  // Fist Upgrade
     ~spstuff/ArFist/CDaffis5.spl~ ~override~  // Fist Upgrade
     ~spstuff/ArFist/CDaffis6.spl~ ~override~  // Fist Upgrade
  SAY NAME1 @716
  SAY NAME2 @716
  SAY UNIDENTIFIED_DESC @716
  SAY DESC @716

COPY ~spstuff/ArFist/CDafnotb.spl~ ~override~  // Disable Bard Song
  SAY NAME1 @717
  SAY NAME2 @717
  SAY UNIDENTIFIED_DESC @717
  SAY DESC @717

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Creslyn's BG2 Item Pack                          \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @500 DESIGNATED 4
REQUIRE_PREDICATE GAME_IS ~soa tob bg2ee eet bgt~ @2 // exclude bgee, iwdee, tutu
LABEL ~cd_spc_creslyn_item_pack~

COPY ~spstuff/creslyn/cdivory.itm~ ~override~  // Ivory Ioun Stone
  SAY NAME1 @502
  SAY NAME2 @502
  SAY UNIDENTIFIED_DESC @501
  SAY DESC @503

COPY ~spstuff/creslyn/cdjuzstf.itm~ ~override~  // Juzam's Crutch
  SAY NAME1 @504
  SAY NAME2 @504
  SAY UNIDENTIFIED_DESC @505
  SAY DESC @505

COPY ~spstuff/creslyn/cdgorstf.itm~ ~override~  // Gorgon Staff
  SAY NAME1 @506
  SAY NAME2 @506
  SAY UNIDENTIFIED_DESC @507
  SAY DESC @507

COPY ~spstuff/creslyn/cdjoorg.itm~ ~override~  // Joorg's Demise
  SAY NAME1 @508
  SAY NAME2 @508
  SAY UNIDENTIFIED_DESC @509
  SAY DESC @509

COPY ~spstuff/creslyn/cdgplate.itm~ ~override~  // Geirthan's Plate
  SAY NAME1 @510
  SAY NAME2 @510
  SAY UNIDENTIFIED_DESC @511
  SAY DESC @511

COPY ~spstuff/creslyn/cdfrcall.itm~ ~override~  // Freedom's Call
  SAY NAME1 @512
  SAY NAME2 @512
  SAY UNIDENTIFIED_DESC @513
  SAY DESC @513

COPY_EXISTING ~temlath.sto~ ~override~  // adds Juzam's Crutch to Temple of Lathander
  ADD_STORE_ITEM CDJuzStf AFTER staf05 #0 #2 #2 ~IDENTIFIED~ #1
  BUT_ONLY

COPY_EXISTING ~wmart1.sto~ ~override~  // adds Joorg's Demise to Joluv
  ADD_STORE_ITEM CDjoorg AFTER waaxe #0 #0 #0 ~IDENTIFIED~ #1
  BUT_ONLY

COPY_EXISTING ~udduer02.sto~ ~override~  // adds Gorgon's Staff to Duergar
  ADD_STORE_ITEM CDgorstf AFTER halb02 #0 #2 #5 ~IDENTIFIED~ #1
  BUT_ONLY

COPY_EXISTING ~wmart2.sto~ ~override~  // adds three items to Dierdre
  ADD_STORE_ITEM CDgplate AFTER wa2plat #0 #0 #0 ~IDENTIFIED~ #1
  ADD_STORE_ITEM CDfrcall AFTER wa2s1h  #0 #2 #0 ~IDENTIFIED~ #1
  ADD_STORE_ITEM CDivory  AFTER wa2helm #0 #0 #0 ~IDENTIFIED~ #1
  BUT_ONLY
  
/////                                                  \\\\\
///// usability adjustments                            \\\\\
/////                                                  \\\\\

// in case the item pack is installed after the kits, this restricts the items properly
ACTION_IF FILE_EXISTS_IN_GAME ~CDsylmys.2da~ THEN BEGIN

  ACTION_IF !enhanced_edition THEN BEGIN

    COPY_EXISTING ~cdjoorg.itm~  ~override~
                  ~cdgplate.itm~ ~override~
      WRITE_BYTE 0x29 (THIS BOR BIT1) // adds helm unusability flag
  
  END ELSE BEGIN  

    OUTER_SET name = RESOLVE_STR_REF (@102)    
    OUTER_SET kitno = IDS_OF_SYMBOL (~kit~ ~CDSylMys~)

    COPY_EXISTING ~cdjoorg.itm~  ~override~
                  ~cdgplate.itm~ ~override~
      LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 319 target = 1 parameter1 = kitno parameter2 = 9 timing = 2 special = name END

  END

END

ACTION_IF FILE_EXISTS_IN_GAME ~CDarcsyl.2da~ THEN BEGIN

  ACTION_IF !enhanced_edition THEN BEGIN

    COPY_EXISTING ~cdjuzstf.itm~ ~override~
                  ~cdgorstf.itm~ ~override~
                  ~cdjoorg.itm~  ~override~
                  ~cdgplate.itm~ ~override~
                  ~cdfrcall.itm~ ~override~
      WRITE_BYTE 0x2f (THIS BOR BIT5) // makes unusable by UD hunters
  
  END ELSE BEGIN  

    OUTER_SET name = RESOLVE_STR_REF (@602)    
    OUTER_SET kitno = IDS_OF_SYMBOL (~kit~ ~CDarcsyl~)

    COPY_EXISTING ~cdjuzstf.itm~ ~override~
                  ~cdgorstf.itm~ ~override~
                  ~cdjoorg.itm~  ~override~
                  ~cdgplate.itm~ ~override~
                  ~cdfrcall.itm~ ~override~
      LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 319 target = 1 parameter1 = kitno parameter2 = 9 timing = 2 special = name END

  END

END

ACTION_IF FILE_EXISTS_IN_GAME ~CDarfist.2da~ THEN BEGIN

  ACTION_IF !enhanced_edition THEN BEGIN

    COPY_EXISTING ~cdjuzstf.itm~ ~override~
                  ~cdgorstf.itm~ ~override~
                  ~cdjoorg.itm~  ~override~
                  ~cdgplate.itm~ ~override~
                  ~cdfrcall.itm~ ~override~
      WRITE_BYTE 0x29 (THIS BOR BIT0) // makes unusable by talos
  
  END ELSE BEGIN  

    OUTER_SET name = RESOLVE_STR_REF (@702)    
    OUTER_SET kitno = IDS_OF_SYMBOL (~kit~ ~CDarfist~)

    COPY_EXISTING ~cdjuzstf.itm~ ~override~
                  ~cdgorstf.itm~ ~override~
                  ~cdjoorg.itm~  ~override~
                  ~cdgplate.itm~ ~override~
                  ~cdfrcall.itm~ ~override~
      LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 319 target = 1 parameter1 = kitno parameter2 = 9 timing = 2 special = name END

  END

END
  
/////                                                  \\\\\
///// ee fixpack-style immunities                      \\\\\
/////                                                  \\\\\
  
ACTION_IF enhanced_edition BEGIN

  OUTER_SET death_immunity        = IDS_OF_SYMBOL (~splstate~ ~DEATH_IMMUNITY~)  
  OUTER_SET disintegrate_immunity = IDS_OF_SYMBOL (~splstate~ ~DISINTEGRATE_IMMUNITY~)   
  OUTER_SET entangle_immunity     = IDS_OF_SYMBOL (~splstate~ ~ENTANGLE_IMMUNITY~)  
  OUTER_SET hold_immunity         = IDS_OF_SYMBOL (~splstate~ ~HOLD_IMMUNITY~)   
  OUTER_SET petrify_immunity      = IDS_OF_SYMBOL (~splstate~ ~PETRIFY_IMMUNITY~) 
  OUTER_SET slow_immunity         = IDS_OF_SYMBOL (~splstate~ ~SLOW_IMMUNITY~)  
  OUTER_SET web_immunity          = IDS_OF_SYMBOL (~splstate~ ~WEB_IMMUNITY~)    
  
  COPY_EXISTING ~cdfrcall.itm~ ~override~  // Freedom's Call (free action but allows haste)
    PATCH_IF (entangle_immunity > 0) BEGIN
      LPF CLONE_EFFECT INT_VAR multi_match = 1 match_opcode = 101 match_parameter2 = 154 opcode = 328 parameter2 = entangle_immunity special = 1 STR_VAR insert = first END // clone immunity to entangle into set spell state
    END  
    PATCH_IF (hold_immunity > 0) BEGIN
      LPF CLONE_EFFECT INT_VAR multi_match = 1 match_opcode = 101 match_parameter2 = 175 opcode = 328 parameter2 = hold_immunity special = 1 STR_VAR insert = first END // clone immunity to hold into set spell state
    END  
    PATCH_IF (slow_immunity > 0) BEGIN
      LPF CLONE_EFFECT INT_VAR multi_match = 1 match_opcode = 101 match_parameter2 = 40 opcode = 328 parameter2 = slow_immunity special = 1 STR_VAR insert = first END // clone immunity to slow into set spell state
    END  
    PATCH_IF (web_immunity > 0) BEGIN
      LPF CLONE_EFFECT INT_VAR multi_match = 1 match_opcode = 101 match_parameter2 = 157 opcode = 328 parameter2 = web_immunity special = 1 STR_VAR insert = first END // clone immunity to web into set spell state
    END  
    BUT_ONLY
  
  COPY_EXISTING ~cdgorstf.itm~ ~override~  // Gorgon Staff (immune to petrification)
    PATCH_IF (petrify_immunity > 0) BEGIN
      LPF CLONE_EFFECT INT_VAR multi_match = 1 match_opcode = 101 match_parameter2 = 134 opcode = 328 parameter2 = petrify_immunity special = 1 STR_VAR insert = first END // clone immunity to petrify into set spell state
    END  
    BUT_ONLY
  
  COPY_EXISTING ~cdjoorg.itm~ ~override~  // Joorg's Demise (5% vorpal hit)
    PATCH_IF (death_immunity > 0) BEGIN
      LPF CLONE_EFFECT INT_VAR multi_match = 1 match_opcode = 13 opcode = 324 parameter1 = death_immunity parameter2 = 110 timing = 0 duration = 0 resist_dispel = 0 savingthrow = 0 STR_VAR insert = first resource = EVAL ~%SOURCE_RES%~ END
    END  
    BUT_ONLY
  
  COPY_EXISTING ~cdgplate.itm~ ~override~  // Geirthan's Plate (death & disintegrate imunity)
    PATCH_IF (death_immunity > 0) BEGIN
      LPF CLONE_EFFECT INT_VAR multi_match = 1 match_opcode = 101 match_parameter2 = 13 opcode = 328 parameter2 = death_immunity special = 1 STR_VAR insert = first END // clone immunity to death magic into set spell state
    END  
    PATCH_IF (disintegrate_immunity > 0) BEGIN
      LPF CLONE_EFFECT INT_VAR multi_match = 1 match_opcode = 101 match_parameter2 = 238 opcode = 328 parameter2 = disintegrate_immunity special = 1 STR_VAR insert = first END // clone immunity to disintegrate into set spell state
    END  
    BUT_ONLY
  
END